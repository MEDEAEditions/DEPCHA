<?xml version="1.0" encoding="UTF-8"?>
<!-- 
    Project: GAMS Projekttemplate
    Company: ZIM-ACDH (Zentrum fÃ¼r Informationsmodellierung - Austrian Centre for Digital Humanities)
 -->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="2.0"
    xmlns:i18n="http://apache.org/cocoon/i18n/2.1"
    xmlns:s="http://www.w3.org/2001/sw/DataAccess/rf1/result" xmlns="http://www.w3.org/1999/xhtml"
    xmlns:t="http://www.tei-c.org/ns/1.0"
    xmlns:owl="http://www.w3.org/2002/07/owl#"
    xmlns:oai_dc="http://www.openarchives.org/OAI/2.0/oai_dc/"
    xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#"
    xmlns:rdfs="http://www.w3.org/2000/01/rdf-schema#"
    xmlns:dc="http://purl.org/dc/elements/1.1/" exclude-result-prefixes="#all"
    xmlns:skos="http://www.w3.org/2004/02/skos/core#">
    
    <xsl:output method="xml" doctype-system="about:legacy-compat" encoding="UTF-8" indent="no"/>
    <xsl:include href="medea-static.xsl"/>
    
    <!-- ///////////////////////////////////////////////////////////// -->
    <!-- ToDo Explanation -->
    
    <xsl:template name="content">
        <section class="row equalheight">
            <article class="col-md-12 col-sm-12 panel">
                <!-- ///////////////////////////////////////////////////////////// -->
                <!-- HEADER -->
                <div class="col-md-12">
                    <h1>
                        <xsl:value-of select="rdf:RDF/owl:Ontology/dc:title"/>
                    </h1>
                    <div class="row">
                        <div class="col-md-6">
                            <h2>The Datamodel</h2>
                            <p>
                                <xsl:value-of select="rdf:RDF/owl:Ontology/owl:versionInfo"/>
                                
                            </p>
                            <p>
                                <xsl:value-of select="rdf:RDF/owl:Ontology/dc:description"/>
                            </p>
                        </div>
                         <div class="col-md-6">
                            <h2>Table of Content</h2>
                             <a href="../../datastreams/ONTOLOGY/content">RDF-Source</a>
                             <ul>
                                 <li>
                                     <a href="#Classes" onclick="scrolldown(this)">Classes</a>
                                 </li>
                                 <li>
                                     <a href="#Properties" onclick="scrolldown(this)">Properties</a>
                                 </li>
                                 <li>
                                     <a href="#Visualisation" onclick="scrolldown(this)">Visualisation</a>
                                 </li>
                             </ul>
                         </div>
                    </div>
                </div>
                
                <!-- ///////////////////////////////////////////////////////////// -->
                <!-- CONTENT-->
                <!-- ///////////////////////////////////////////////////////////// -->
                <div class="col-md-12" id="Classes" style="margin-top:50px">
                    <h1>Classes</h1>
                    <div class="list-group list-group-root">
                        <!-- CLASSES without CIDOC-superclass -->
                        <xsl:for-each-group select="//rdfs:Class[@rdf:about][not(contains(rdfs:subClassOf, 'http://www.cidoc-crm.org/cidoc-crm'))]" group-by="(@rdf:about)" >
                           <div  style="padding-bottom:10px">
                                <h3 id="{substring-after(current-grouping-key(), '#')}" class="list-group-item">
                                    <xsl:value-of select="rdfs:label[@xml:lang='en']"/>
                                </h3>
                                <!-- create TABLE for current class -->
                                <xsl:call-template name="createTable"/>
                           </div>
                                <!-- for each SUBCLASSES of the current class -->
                                <xsl:if test="//rdfs:Class[@rdf:about][rdfs:subClassOf/@rdf:resource = current-grouping-key()]">
                                    <xsl:for-each-group select="//rdfs:Class[@rdf:about][rdfs:subClassOf/@rdf:resource = current-grouping-key()]" group-by="(@rdf:about)" >
                                        <div  style="padding-bottom:10px">
                                            <h3 id="{substring-after(current-grouping-key(), '#')}" class="list-group-item">
                                                <xsl:value-of select="rdfs:label[@xml:lang='en']"/>
                                            </h3>
                                            <xsl:call-template name="createTable"/>
                                        </div>
                                    </xsl:for-each-group>
                                </xsl:if>
                          
                        </xsl:for-each-group>
                        
                    </div>
                </div>
                <!-- ///////////////////////////////////////////////////////////// -->
                <div class="col-md-12" id="Properties" style="margin-top:50px">
                    <h1>Properties</h1>
                    <div class="list-group list-group-root">
                        <!-- CLASSES without CIDOC-superclass -->
                        <xsl:for-each-group select="//rdf:Property[@rdf:about]" group-by="(@rdf:about)" >
                            <div  style="padding-bottom:10px">
                                <h3 id="{substring-after(current-grouping-key(), '#')}" class="list-group-item">
                                    <xsl:value-of select="rdfs:label[@xml:lang='en']"/>
                                </h3>
                                <!-- create Table -->
                                <xsl:call-template name="createTable"/>
                            </div>
                        </xsl:for-each-group>
                        
                    </div>
                </div>
                <!-- ///////////////////////////////////////////////////////////// -->
                <div class="row" id="Visualisation" style="margin-top:50px">
                    <h1>Visualisation</h1>
                    <embed 
                        src="http://visualdataweb.de/webvowl/#iri=http://glossa.uni-graz.at/archive/objects/o:medea.bookkeeping/datastreams/ONTOLOGY/content"
                        height="1000px"
                        width="100%"/>
                </div>
            </article>
        </section>
    </xsl:template>
    
    <!-- ///////////////////////////////////////////////////////////// -->
    <!-- createTable -->
    <xsl:template name="createTable">
        <table class="table table-bordered" id="{current-grouping-key()}">
            <tbody>
                <xsl:if test="rdfs:subClassOf">
                    <tr>
                        <td class="col-md-2" style="font-weight: bold;">Subclass of</td>
                        <td class="col-md-10">
                            <xsl:value-of select="rdfs:subClassOf/@rdf:resource"/>
                        </td>
                    </tr>
                </xsl:if>
                <tr>
                    <td class="col-md-2" style="font-weight: bold;">Description</td>
                    <td class="col-md-10">
                        <xsl:value-of select="rdfs:comment[@xml:lang='en']"/>
                    </td>
                </tr>
                <xsl:if test="skos:example">
                 <tr>
                     <td class="col-md-2" style="font-weight: bold;">Usage</td>
                     <td class="col-md-10">
                         <xsl:call-template name="createExample"/>
                     </td>
                 </tr>
                </xsl:if>
                <xsl:if test="rdfs:seeAlso/@rdf:resource">
                <tr>
                    <td class="col-md-2" style="font-weight: bold;">seeAlso</td>
                    <td class="col-md-10">
                        <xsl:for-each select="rdfs:seeAlso/@rdf:resource">
                            <a href="{.}">
                                <xsl:value-of select="substring-before(substring-after(.,'.'), '/')"/>
                            </a>
                        </xsl:for-each>
                    </td>
                </tr>
                </xsl:if>
                <xsl:variable name="Domain" select="rdfs:domain/@rdf:resource"/>
                <xsl:if test="$Domain">
                    <tr>
                        <td class="col-md-2" style="font-weight: bold;">Domain</td>
                        <td class="col-md-10"> 
                         <xsl:for-each select="//rdfs:Class[@rdf:about = $Domain]/rdfs:label[@xml:lang='en']">
                             <xsl:value-of select="."/><xsl:text>; </xsl:text>
                         </xsl:for-each>
                     </td>
                    </tr>
                </xsl:if>
                <xsl:variable name="Range" select="rdfs:range/@rdf:resource"/>
                <xsl:if test="$Range">
                <tr>
                    <td class="col-md-2" style="font-weight: bold;">Range</td>
                    <td class="col-md-10">
                        <xsl:choose>
                            <xsl:when test="//rdfs:Class[@rdf:about = $Range]/rdfs:label[@xml:lang='en']">
                                <xsl:for-each select="//rdfs:Class[@rdf:about = $Range]/rdfs:label[@xml:lang='en']">
                                    <xsl:value-of select="."/><xsl:text>; </xsl:text>
                                </xsl:for-each>
                            </xsl:when>
                            <xsl:otherwise>
                                <xsl:value-of select="$Range"/>
                            </xsl:otherwise>
                        </xsl:choose>
                        
                    </td>
                </tr>
                </xsl:if>
            </tbody>
        </table>
    </xsl:template>
    
    <!-- ///////////////////////////////////////////////////////////// -->
    <!-- createExample -->
    <xsl:template name="createExample">
        <h4 style="font-weight: bold;">TEI</h4>
        <xsl:for-each select="skos:example[@rdf:parseType]">
            <div class="well">
                <xsl:apply-templates select="./element()"/>
                <xsl:text> </xsl:text>
            </div>
        </xsl:for-each>
        <h4 style="font-weight: bold;">CSV</h4>
        <xsl:apply-templates select="skos:example[not(@rdf:parseType)]"/>
    </xsl:template>
    
    <!-- ///////////////////////////////////////////////////////////// -->
    <!-- element has  @ -->
    <!-- styles all elements with attributes for examples and usage -->
    <xsl:template match="element()[@*]">
        <span style="padding-left:30px;"><span style="color:blue;"><xsl:text>&lt;</xsl:text><xsl:value-of select="name()"/><xsl:text> </xsl:text></span>
        <xsl:for-each select="@*">
            <span style="color:orange;"><xsl:value-of select="name(.)"/>
                <xsl:text> = </xsl:text>
            </span>
            <span style="color:rgb(150, 0, 0);"><xsl:text>"</xsl:text>
            <xsl:value-of select="."/><xsl:text>" </xsl:text></span>
        </xsl:for-each>
        <xsl:text>&gt;</xsl:text>
        <xsl:apply-templates></xsl:apply-templates>
        <span style="color:blue;"><xsl:text>&lt;/</xsl:text><xsl:value-of select="name()"/><xsl:text>&gt;</xsl:text></span></span>
    </xsl:template>
    
    <!-- ///////////////////////////////////////////////////////////// -->
    <!-- element has no @ -->
    <!-- styles all elements without attributes for examples and usage -->
    <xsl:template match="element()">
        <xsl:if test="not(position() = 1)">
            <br/>
        </xsl:if>
        <span style="color:blue;"><xsl:text>&lt;</xsl:text>
            <xsl:value-of select="name()"/>
        <xsl:text>&gt;</xsl:text></span>
        <br/>
        <xsl:apply-templates/>
        <br/>
        <span style="color:blue;"><xsl:text>&lt;/</xsl:text>
            <xsl:value-of select="name()"/>
        <xsl:text>&gt;</xsl:text></span>
    </xsl:template>
    
    <!-- ///////////////////////////////////////////////////////////// -->
    <!-- for every CSV-Example -->
    <xsl:template match="skos:example[not(@rdf:parseType)]">
        <div class="well">
        <b><xsl:value-of select="substring-before(., ' ')"/></b><br/>
        <xsl:value-of select="substring-after(., ' ')"/>
        </div>
        <xsl:text> </xsl:text>
    </xsl:template>
    
</xsl:stylesheet>