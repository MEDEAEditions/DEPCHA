<?xml version="1.0" encoding="UTF-8"?>
<!-- 
    Project: GAMS Projekttemplate
    Company: ZIM-ACDH (Zentrum fÃ¼r Informationsmodellierung - Austrian Centre for Digital Humanities)
    Author: Hans Clausen, Maximilian MÃ¼ller, Gerlinde Schneider, Martina Semlak, Christian Steiner, Elisabeth Steiner
    Last update: 2015
 -->




<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:functx="http://www.functx.com"
    xmlns:s="http://www.w3.org/2001/sw/DataAccess/rf1/result" xmlns="http://www.w3.org/1999/xhtml"
    xmlns:t="http://www.tei-c.org/ns/1.0" xmlns:oai_dc="http://www.openarchives.org/OAI/2.0/oai_dc/"
    xmlns:dc="http://purl.org/dc/elements/1.1/" exclude-result-prefixes="#all">
     
    <xsl:strip-space elements="*"/>
    <xsl:include href="medea-static.xsl"/>
    
    <!-- VARIABLES -->   
    <!-- //////////////////////////////// -->
    <xsl:variable name="TEI_HEADER" select="//t:teiHeader"/>
    
    <xsl:output method="xml" doctype-system="about:legacy-compat" encoding="UTF-8" indent="no"/>
    
    
    <xsl:template name="content">
        
        <!-- /// PAGE-CONTENT /// -->
        <!-- https://bootsnipp.com/snippets/featured/list-grid-view -->
<!--        <script>
            $(document).ready(function() {
            $('#text').click(function(event){event.preventDefault();$('#scan_resouce .item').removeClass('spreadsheet-group-item');$('#scan_resouce .item').addClass('list-group-item');});
            $('#spreadsheet').click(function(event){event.preventDefault();$('#scan_resouce .item').removeClass('list-group-item');$('#scan_resouce .item').addClass('spreadsheet-group-item');});
            });
        </script>-->
        
        <script>
            $(document).ready(function() {
            $('#text_button').click(function(event){event.preventDefault();$('.spreadsheet_entry').addClass('hidden');$('.text_entry').removeClass('hidden');});
            $('#spreadsheet_button').click(function(event){event.preventDefault();$('.text_entry').addClass('hidden');$('.spreadsheet_entry').removeClass('hidden');});
            });
        </script>
        
        <section class="row equalheight">
            
            <!-- ///NAVIGATION/// -->
            <article class="col-md-3 equal panel">
                    <div class="navigation">
                        <h3>Navigation</h3>
                        <div class="pre-scrollable">
                        <xsl:for-each select="//t:head | t:front">
                            <xsl:sort select="."></xsl:sort>
                            
                            <p class="list-group-item-text">
                                <a  href="{concat('#', 'head-', generate-id())}" class="list-group-item" onclick="scrolldown(this)">
                                  <xsl:value-of select="."/>
                                  <xsl:if test=".//@when">
                                     <xsl:text> - </xsl:text>
                                     <xsl:value-of select=".//@when"/>
                                  </xsl:if>
                                </a>
                            </p>        
                        </xsl:for-each> 
                        </div>
                    </div>
            </article>
            
            <!-- ///CONTENT/// -->
            <article class="col-md-9 equal">
                <div class="container panel">
                    <!-- HEADER-->
                    <div class="header">
                        <h3>
                            <xsl:value-of select="$TEI_HEADER/t:fileDesc/t:titleStmt/t:title"/>
                        </h3>
                        <div class="well well-sm">
                            <div class="btn-group">
                                <a href="#" id="text_button" class="btn btn-default btn-sm">
                                    <span class="glyphicon glyphicon-th-list"><xsl:text> </xsl:text></span>
                                    <xsl:text> Text</xsl:text></a> 
                                
                                <a href="#" id="spreadsheet_button" class="btn btn-default btn-sm">
                                    <span class="glyphicon glyphicon-th"><xsl:text> </xsl:text></span>
                                    <xsl:text> Spreadsheet</xsl:text>
                                </a>
                                
                                <a class="button" href="{concat('http://glossa.uni-graz.at/',$teipid,'/TEI_SOURCE')}" role="button" style="margin: 10px;" target="_blank">
                                    <img alt="TEI" height="25" id="tei" src="http://glossa.uni-graz.at/templates/img/tei_icon.jpg" title="TEI"/>
                                </a>
                                <a class="button" href="{concat('http://glossa.uni-graz.at/',$teipid,'/RDF')}" role="button" style="margin: 10px;" target="_blank">
                                    <img alt="RDF" height="25" id="rdf" src="http://glossa.uni-graz.at/templates/img/RDF_icon.png" title="RDF"/>
                                </a>
                                <a class="button" href="{concat('http://glossa.uni-graz.at/archive/objects/',$teipid, '/methods/sdef:TEI/getLaTeXPDF')}" role="button" style="margin: 10px;">
                                    <img alt="PDF" height="25" id="pdf" src="http://glossa.uni-graz.at/templates/img/pdf_icon.png" title="PDF"/>
                                </a>
                                <a class="button" href="{concat('http://glossa.uni-graz.at/archive/objects/',$teipid,'/methods/sdef:TEI/getHSSF')}" role="button" style="margin: 10px;">
                                    <img alt="RDF" height="25" id="rdf" src="/gamsdev/pollin/medea/trunk/www/img/TABELLENSYMBOL.png" title="HSSF"/>
                                </a>

                            </div>
                        </div>                 
                    </div>
                    
                    <!-- CONTENT, TEXT -->
                    <div class="content col-md-12">
                        <xsl:apply-templates select="//t:text"/> 
                    </div>
                </div>
            </article>
        </section>
    </xsl:template>
    
    
    
    
    
    <xsl:template match="node()[tokenize(@ana, ' ') = '#bk_entry']">
        <!--<xsl:apply-templates></xsl:apply-templates>-->
        
        <div class="row">
        <div class="bk_entry text_entry col-md-12" id="{concat('Entry-', position())}">
            
            <div class="col-md-8">
                <xsl:apply-templates></xsl:apply-templates>
            </div>
            <div class="col-md-4">
                <label class="pull-right"><input onclick="" type="checkbox" class=""/><xsl:text> </xsl:text></label>
            </div>
        </div>
        </div>
        <div class="row">
        <table class="table hidden spreadsheet_entry">
            <tbody>
                <tr>
                   <!-- <xsl:for-each select=".//.[tokenize(@ana, ' ') = '#bk_amount']">-->
                      <xsl:variable name="FromSpreadsheet">
                         <xsl:choose>
                             <xsl:when test="@ref">
                                 <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_from']/@ref"/>
                             </xsl:when>
                             <xsl:when test="@corresp">
                                 <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_from']/@corresp"/>
                             </xsl:when>
                             <xsl:otherwise>
                                 <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_from']"/>
                             </xsl:otherwise>
                         </xsl:choose> 
                      </xsl:variable>
                    
                     <xsl:variable name="ToSpreadsheet">
                         <xsl:choose>
                             <xsl:when test="@ref">
                                 <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_to']/@ref"/>
                             </xsl:when>
                             <xsl:when test="@corresp">
                                 <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_to']/@corresp"/>
                             </xsl:when>
                             <xsl:otherwise>
                                 <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_to']"/>
                             </xsl:otherwise>
                         </xsl:choose>
                     </xsl:variable>
                      

                        <td class="col-md-4">
                            <xsl:value-of select="$FromSpreadsheet"/>
                            <xsl:text> </xsl:text><span class="glyphicon glyphicon-arrow-right"><xsl:text> </xsl:text></span><xsl:text> </xsl:text>
                            <xsl:value-of select="$ToSpreadsheet"/>
                        </td>
                        <td class="col-md-2">
                            <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_amount']//@quantity"/><xsl:text> </xsl:text>                       
                        </td>
                        <td class="col-md-2"> 
                            <xsl:value-of select=".//.[tokenize(@ana, ' ') = '#bk_amount']//@unit"/><xsl:text> </xsl:text>  
                        </td>
                        <td class="col-md-2"> 
                            <label class="pull-right"><input onclick="" type="checkbox" class=""/><xsl:text> </xsl:text></label>
                        </td>
                     
                    <!--</xsl:for-each>--> 
                </tr>
                
                <xsl:if test="position() = last()">
                    <tr>
                        <td class="col-md-2">
                            <b>SUMME</b><xsl:text> </xsl:text>                     
                        </td>
                        <td class="col-md-2">
                            <xsl:sequence select="sum(.//@quantity/number(translate(., ',', '.')))"/>     
                        </td>
                        <td class="col-md-2">
                            <xsl:text> </xsl:text>                       
                        </td>
                    </tr>
                </xsl:if>
                
            </tbody>
        </table>
        </div>
    </xsl:template>
    
    
    
    <!-- ///TEXT ///  -->
    <xsl:template match="t:p[@ana = '#bk_total']">
        <div class="row">
            <xsl:apply-templates></xsl:apply-templates>
        </div>
    </xsl:template>
    

    <xsl:template match="t:pb">
        <div class="page col-md-12 row">
            <h4>
             <xsl:choose>
                 <xsl:when test="@facs">
                     <xsl:value-of select="@facs"/>
                 </xsl:when>
                 <xsl:when test="@n">
                     <xsl:value-of select="@n"/>
                 </xsl:when>
                 <xsl:otherwise></xsl:otherwise>
             </xsl:choose>
            </h4>
            <hr/>

        </div>
    </xsl:template>
    

    <!-- //////////////////// -->
    
    <xsl:template match="t:text">
        <xsl:apply-templates/>
    </xsl:template>
    
    
    <xsl:template match="t:body">
        <xsl:apply-templates/>
    </xsl:template>
    

    
    <xsl:template match="t:head | t:front">
        <h4 class="bk_head" id="{concat('head-', generate-id())}">
            <xsl:apply-templates/>
        </h4>
    </xsl:template>
    
    <xsl:template match="t:date">
        <a data-toggle="tooltip">
            <xsl:attribute name="title">
                <xsl:choose>
                    <xsl:when test="@when">
                        <xsl:value-of select="@when"/>
                    </xsl:when>
                    <xsl:when test="@from">
                        <xsl:value-of select="@from"/>
                        <xsl:text> to </xsl:text>
                        <xsl:value-of select="@to"/>
                    </xsl:when>
                </xsl:choose>
            </xsl:attribute>
            <xsl:apply-templates/>
        </a>
    </xsl:template>
   
    <xsl:template match="t:closer">
        <div class="bk_closer text_entry">
            <xsl:apply-templates/>
        </div>
    </xsl:template>
    
    <!-- DEL, ADD etc. -->
    <!-- /////////////////////////////////////// -->
    <xsl:template match="t:del">
        <del class="bg-warning" data-toggle="tooltip" title="deletion">
            <xsl:apply-templates/>
        </del>
    </xsl:template>
    
    <xsl:template match="t:damage">
        <mark class="bg-danger" data-toggle="tooltip" title="damage">
            <xsl:apply-templates/>
        </mark> 
    </xsl:template>
    
    <xsl:template match="t:supplied">
        <mark data-toggle="tooltip" title="supplied">
            <xsl:apply-templates/>
        </mark>
    </xsl:template>
    
    <xsl:template match="t:add">
        <mark class="bg-success" data-toggle="tooltip" title="add">
            <xsl:apply-templates/>
        </mark>
    </xsl:template>
    
    <xsl:template match="t:subst">
        <xsl:apply-templates/>
    </xsl:template>
    
    <xsl:template match="t:note">
        <xsl:text> </xsl:text>
        <mark class="glyphicon glyphicon-star" data-toggle="tooltip" title="{normalize-space(.)}"><xsl:text> </xsl:text></mark>
    </xsl:template>
    
    <xsl:template match="t:hi">
        <em>
            <xsl:apply-templates></xsl:apply-templates>
        </em>
    </xsl:template>
   
    <xsl:template match="t:choice">
        <xsl:text> </xsl:text>
        <a href="#" data-toggle="tooltip" title="{t:expan}">
            <xsl:choose>
                <xsl:when test="t:abbr">
                    <xsl:value-of select="t:abbr"/>
                </xsl:when>
                <xsl:otherwise><xsl:comment>t:abbr missing in choice</xsl:comment></xsl:otherwise>
            </xsl:choose>   
        </a>
        <xsl:text> </xsl:text>
    </xsl:template>
    
    <!-- fw comes always after a pb, what is it meaning? is pb @n the same? -->
    <xsl:template match="t:fw">
    </xsl:template>


</xsl:stylesheet>